#+BEGIN_SRC gnuplot :var data=datosPractica4.org:diagrama-termogravimetrico :exports results :eval yes
  reset

  set term epslatex color colortext font ',11'
  set output 'diagramaTermogravimetrico.tex'
  set size 1,2
  set autoscale
  set lmargin 0.5
  set rmargin 0.5
  set encoding utf8
  set decimalsign '.'
  set format x '$%.f$'
  set format y '$%.f$'
  set title '\bf Diagrama termogravim√©trico del N_2'

  set xlabel '\shortstack{Tiempo, $t$/ s\\ \,}'
  set xrange [0:960]
  set xtics 0,120,960

  set ylabel 'Masa de N$_2$ evaporado, $m^v$/ g'
  set yrange [0:90]
  set ytics 0,15,90

  set tics scale 0,0.1

  set mxtics 4
  set mytics 3
  set style line 13 lc rgb '#ddccdd'
  set style line 12 lc rgb '#aabbaa'
  set grid xtics mxtics ytics mytics back ls 12, ls 13

  y45(x) = m45*x + m45i
  fit y45(x) data u 1:3 via m45,m45i
  y40(x) = m40*x + m40i
  fit y40(x) data u 1:4 via m40,m40i
  y30(x) = m30*x + m30i
  fit y30(x) data u 1:5 via m30,m30i
  y25(x) = m25*x + m25i
  fit y25(x) data u 1:6 via m25,m25i
  y0(x) = m0*x + m0i
  fit y0(x) data u 1:7 via m0,m0i

  stats data u 1:3 name 'R45'
  stats data u 1:4 name 'R40'
  stats data u 1:5 name 'R30'
  stats data u 1:6 name 'R25'
  stats data u 1:7 name 'R0'

  str45 = (gprintf('$m^v_{45}/$ g $= %.1f$',m45i).gprintf('$\, +\, %.2e\, t/$ s',m45))
  set label str45 at 240,12 rotate by 14 front textcolor 'blue'
  str40 = (gprintf('$m^v_{40}/$ g $= %.1f$',m40i).gprintf('$\, +\, %.2e\, t/$ s',m40))
  set label str40 at 240,35 rotate by 12 front textcolor 'dark-green'
  str30 = (gprintf('$m^v_{30}/$ g $= %.1f$',m30i).gprintf('$\, +\, %.2e\, t/$ s',m30))
  set label str30 at 240,55 rotate by 9 front textcolor 'dark-magenta'
  str25 = (gprintf('$m^v_{25}/$ g $= %.1f$',m25i).gprintf('$\, +\, %.2e\, t/$ s',m25))
  set label str25 at 240,71 rotate by 8 front textcolor 'dark-orange'
  str0 = (gprintf('$m^v_0/$ g $= %.1f$',m0i).gprintf('$\, +\, %.2e\, t/$ s',m0))
  set label str0 at 240,83 rotate by 6 front textcolor 'red'

  set key box linestyle -1
  set key samplen 0.01
  set key bmargin center
  set key Left
  set key spacing 2
  set key width -5
  set key height 0
  set key reverse
  set key invert

  plot data u 1:3 w p ps 1 pt 7 lc 'blue' title (gprintf('Datos experimentales ($I = 0.45$A), $R^2 = %.4f$',R45_correlation**2)), \
       y45(x) w l lw 2 lc 'black' notitle, \
       data u 1:4 w p ps 1 pt 7 lc 'dark-green' title (gprintf('Datos experimentales ($I = 0.40$A), $R^2 = %.5f$',R40_correlation**2)), \
       y40(x) w l lw 2 lc 'black' notitle, \
       data u 1:5 w p ps 1 pt 7 lc 'dark-magenta' title (gprintf('Datos experimentales ($I = 0.30$A), $R^2 = %.5f$',R30_correlation**2)), \
       y30(x) w l lw 2 lc 'black' notitle, \
       data u 1:6 w p ps 1 pt 7 lc 'dark-orange' title (gprintf('Datos experimentales ($I = 0.25$A), $R^2 = %.5f$',R25_correlation**2)), \
       y25(x) w l lw 2 lc 'black' notitle, \
       data u 1:7 w p ps 1 pt 7 lc 'red' title (gprintf('Datos experimentales ($I = 0$A), $R^2 = %.4f$',R0_correlation**2)), \
       y0(x) w l lw 2 lc 'black' notitle

  unset output
#+END_SRC

#+RESULTS:
